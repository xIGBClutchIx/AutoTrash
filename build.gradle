plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
    id 'com.diffplug.spotless' version '6.25.0'
}

ext {
    def home = System.getProperty('user.home')
    def os = System.getProperty('os.name').toLowerCase()

    if (os.contains('win')) {
        hytaleHome = "${home}/AppData/Roaming/Hytale"
    } else if (os.contains('mac')) {
        hytaleHome = "${home}/Library/Application Support/Hytale"
    } else {
        hytaleHome = "${home}/.config/Hytale"
    }

    java_version = project.findProperty('java_version') ?: '25'
    patchline = project.findProperty('patchline') ?: 'release'
    includes_pack = project.findProperty('includes_pack') ?: 'false'
    load_user_mods = project.findProperty('load_user_mods') ?: 'false'
}

repositories {
    mavenCentral()
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version.toInteger())
    withSourcesJar()
    withJavadocJar()
}

spotless {
    java {
        eclipse().configFile('eclipse-formatter.xml')
        formatAnnotations()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

dependencies {
    def installServerJar = file("$hytaleHome/install/$patchline/package/game/latest/Server/HytaleServer.jar")
    def bundledServerJar = file('libraries/HytaleServer.jar')
    def serverJar = installServerJar.exists() ? installServerJar : bundledServerJar
    implementation(files(serverJar))
}

def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}

tasks.register('updatePluginManifest') {
    def manifestFile = file('src/main/resources/manifest.json')
    doLast {
        if (!manifestFile.exists()) {
            throw new GradleException("Could not find manifest.json at ${manifestFile.path}!")
        }
        def manifestJson = new groovy.json.JsonSlurper().parseText(manifestFile.text)
        manifestJson.Version = version
        manifestJson.IncludesAssetPack = includes_pack.toBoolean()
        manifestFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
    }
}

tasks.named('processResources') {
    dependsOn 'updatePluginManifest'
}

idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = "--allow-op --assets=$hytaleHome/install/$patchline/package/game/latest/Assets.zip"
        if (includes_pack.toBoolean()) {
            programParameters += " --mods=${sourceSets.main.java.srcDirs.first().parentFile.absolutePath}"
        }
        if (load_user_mods.toBoolean()) {
            programParameters += " --mods=$hytaleHome/UserData/Mods"
        }
        workingDirectory = serverRunDir.absolutePath
    }
}
